giOSC OSCinit $port

instr receiver

kMessage init 0
SInstrument init ""
kInstance init 0
kDelay init 0
kDuration init 0
SParameters init ""

kMessage OSClisten giOSC, "oscilla", "sfffs", SInstrument, kInstance, kDelay, kDuration, SParameters

if kMessage == 1 then

SScore sprintfk "i \"scorer\" 0 0 \"%s\" %f %f %f \"%s\"", SInstrument, kInstance, kDelay, kDuration, SParameters

scoreline SScore, 1

endif

endin

instr scorer

SInstrument strget p4
iInstrument nstrnum SInstrument

iInstance = p5

if iInstance > 0 && iInstance < 1 then

iInstrument += iInstance

endif

iDelay = p6
iDuration = p7
SParameters strget p8

SScore sprintf "i %f %f %f %s", iInstrument, iDelay, iDuration, SParameters

scoreline_i SScore

endin

schedule "receiver", 0, -1
